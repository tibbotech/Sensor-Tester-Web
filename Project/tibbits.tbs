include "global.tbh"

#define DELAY_50MS (5)

const TBT28_INIT_SIGNATURE=&h5982
const TBT28_STAMP="TBT28> "
const TBT28_CR_LF=chr(13)+chr(10)
const TBT28_WRITE_ADDR=&h46		' address code for write
const TBT28_READ_ADDR=&h47		' address code for read
const TBT28_CMD_POWER_DOWN		=&h00
const TBT28_CMD_POWER_ON		=&h01
const TBT28_CMD_AUTO_RESOL_0	=&h10
const TBT28_CMD_HRESOL_0		=&h12
const TBT28_CMD_LRESOL_0		=&h13



declare sub tbt28_cmd_send(cmd as byte)
dim i2c_num_tbt28 as byte
dim tbt28_init_flag as word

const TBT29_INIT_SIGNATURE=&hF083
const TBT29_STAMP="TBT29> "
const TBT29_CR_LF=chr(13)+chr(10)

const TBT29_DEVID		=&h400
const TBT29_WRITE_ADDR=&h30
const TBT29_READ_ADDR=&h31
enum tbt29_resolution
	TBT29_RESOLUSION_MODE_1=&h01  '0.25C (tCONV = 65 ms typical)
end enum

declare sub tbt29_write_data(op as tbt29_regs,data as word)
declare sub delay_msecond(value as word)
dim i2c_num_tbt29 as byte
dim tbt29_init_flag as word

const TBT30_INIT_SIGNATURE=&h3969
const TBT30_STAMP="TBT30> "
const TBT30_CR_LF=chr(13)+chr(10)
const TBT30_WRITE_ADDR=&h4E
const TBT30_READ_ADDR=&h4F
enum tbt30_status
	TBT30_STATUS_NORMAL_OPERATION=&h0,
	TBT30_STAUTS_STALE_DATA=&h1,
	TBT30_STAUTS_IN_COMMAND_MODE=&h2,
	TBT30_STAUTS_NOT_USED=&h3
end enum


dim i2c_num_tbt30 as byte
dim tbt30_init_flag as word

const TBT35_INIT_SIGNATURE=&h10FE
const TBT35_STAMP="TBT35>"
const TBT35_CR_LF=chr(13)+chr(10)

const TBT35_WRITE_ADDR=&hC0			' Manufacturer address code for write
const TBT35_READ_ADDR =&hC1			' Manufacturer address code for read

const TBT35_CMD_READ_PRESSURE		=&h00
const TBT35_CMD_READ_COEFFICIENT	=&h04
const TBT35_CMD_START_CONVERSION	=&h12

dim i2c_num_tbt35 as byte
dim tbt35_init_flag as word
dim a0,b1,b2,c12 as float=0

const TBT36_INIT_SIGNATURE=&h1590
const TBT36_STAMP="TBT36> "
const TBT36_CR_LF=chr(13)+chr(10)

const TBT36_WRITE_ADDR			=&hA6			' Write Addr
const TBT36_READ_CODE 			=&hA7			' Read Addr

const TBT36_DEVID				=&hE5

const TBT36_REG_DEVID			=&h00
const TBT36_REG_THRESH_TAP		=&h1d
const TBT36_REG_OFS_X			=&h1E
const TBT36_REG_OFS_Y			=&h1F
const TBT36_REG_OFS_Z			=&h20
const TBT36_REG_DUR				=&h21
const TBT36_REG_LATENT			=&h22
const TBT36_REG_WINDOW			=&h23
const TBT36_REG_THRESH_ACT		=&h24
const TBT36_REG_THRESH_INACT	=&h25
const TBT36_REG_TIME_INCAT		=&h26
const TBT36_REG_ACT_INACT_CTL	=&h27
const TBT36_REG_THRESH_FF		=&h28
const TBT36_REG_TIME_FF			=&h29
const TBT36_REG_TAP_AXES		=&h30
const TBT36_REG_ACT_TAPSTATUS	=&h2B
const TBT36_REG_BW_RATE			=&h2C
const TBT36_REG_POWER_CTL		=&h2D
const TBT36_REG_INT_ENABLE		=&h2E
const TBT36_REG_INT_MAP			=&h2F
const TBT36_REG_INT_SOURCE		=&h30
const TBT36_REG_DATA_FORMAT		=&h31
const TBT36_REG_DATA_X0			=&h32
const TBT36_REG_DATA_X1			=&h33
const TBT36_REG_DATA_Y0			=&h34
const TBT36_REG_DATA_Y1			=&h35
const TBT36_REG_DATA_Z0			=&h36
const TBT36_REG_DATA_Z1			=&h37
const TBT36_REG_FIFO_CTL		=&h38
const TBT36_REG_FIFO_STATUS		=&h39
const TBT36_REG_TA_SIGN			=&h3A
const TBT36_REG_ORIENT_CONF		=&h3B
const TBT36_REG_ORIENT			=&h3C

declare sub tbt36_write_reg(cmd as byte, data as byte)
declare function tbt36_get_reg(cmd as byte) as byte

dim format as byte
dim i2c_num_tbt36 as byte
dim tbt36_init_flag as word

'====================================================================
function tbt28_init(use_ssi as no_yes)as ok_ng
'Call this function to initialize the Tibbit.
'Use_ssi argument specifies whether you want to communicate with this Tibbit using the SSI channel running in the I2C mode (YES), or
'go for direct I/O manipulation a.k.a. bit-banging (NO).
'Note that there is a limited number of SSI channels so if you set use_ssi=YES this function may return NG, meaning that
'there are no free SSI channels left. This doesn't mean failure. Rather, it means that comms will proceed in bit banging mood.
	tbt28_init=OK
	tbt28_init_flag=TBT28_INIT_SIGNATURE

	i2c_num_tbt28=si2c_register("TBT28", DATA_PIN, CLK_PIN, use_ssi)

	si2c_get(i2c_num_tbt28)

	if i2c_num_tbt28<=3 then
		ssi.channel=i2c_num_tbt28
		ssi.enabled=NO
		ssi.baudrate=PL_SSI_BAUDRATES_400kHz
		ssi.clkmap=CLK_PIN
		ssi.dimap=DATA_PIN
		ssi.domap=DATA_PIN
		ssi.direction=PL_SSI_DIRECTION_LEFT
		ssi.zmode=PL_SSI_ZMODE_ENABLED_ON_ZERO
		ssi.mode=PL_SSI_MODE_0
		ssi.enabled=YES
	else
		if use_ssi=YES then
			tbt28_init=NG
		end if
	end if

	'Switch sensor to power down mode
	tbt28_cmd_send(TBT28_CMD_POWER_DOWN)

	'Switch sensor to power on mode
	tbt28_cmd_send(TBT28_CMD_POWER_ON)

	'Switch sensor to high resolution mode
	tbt28_cmd_send(TBT28_CMD_HRESOL_0)

	'Wait to complete 1st Auto-resolution mode measurement.(max. 180 ms)
	delay_msecond(DELAY_50MS+DELAY_50MS+DELAY_50MS)

	tbt28_init=tbt28_check()
	
end function

function tbt28_check() as ok_ng
	tbt28_check=NG
	dim readingsrawdata as byte(2)
	dim b as byte
	'Returns a 16-bit number expressing relative ambient light intensity.
	if tbt28_init_flag<> TBT28_INIT_SIGNATURE then
		exit function
	end if
	si2c_get(i2c_num_tbt28)
	si2c_start()
	si2c_write(TBT28_READ_ADDR)
	readingsrawdata(0)=si2c_read(true)
	readingsrawdata(1)=si2c_read(false)
	si2c_stop()
	for b=0 to 1
		if readingsrawdata(b)<>255 then
			tbt28_check=OK
			exit for
		end if
	next b
	
	current_probe_info.reading=str(readingsrawdata(0)*256+readingsrawdata(1))+" lux"
	
end function


sub tbt28_cmd_send(cmd as byte)
	si2c_start()
	si2c_write(TBT28_WRITE_ADDR)
	si2c_write(cmd)
	si2c_stop()
end sub

function tbt29_init(use_ssi as no_yes) as errcheck
'Call this function to initialize the Tibbit.
'Use_ssi argument specifies whether you want to communicate with this Tibbit using the SSI channel running in the I2C mode (YES), or
'go for direct I/O manipulation a.k.a. bit-banging (NO).
'Note that there is a limited number of SSI channels so if you set use_ssi=YES this function may return TBT29_NO_SSI_AVAILABLE, meaning that
'there are no free SSI channels left. This doesn't mean failure. Rather, it means that comms will proceed in bit banging mood.
'Other error codes (TBT29_WRONG_MFGID and TBT29_WRONG_DEVID_REVISION) indicate failure.

	tbt29_init=TBT29_OK
	tbt29_init_flag=TBT29_INIT_SIGNATURE
	i2c_num_tbt29=si2c_register("TBT29", DATA_PIN, CLK_PIN,use_ssi)

	if i2c_num_tbt29 <4 then
		ssi.channel=i2c_num_tbt29
		ssi.enabled=NO
		ssi.baudrate=PL_SSI_BAUDRATES_400kHz
		ssi.clkmap=CLK_PIN
		ssi.dimap=DATA_PIN
		ssi.domap=DATA_PIN
		ssi.direction=PL_SSI_DIRECTION_LEFT
		ssi.mode=PL_SSI_MODE_2
		ssi.zmode=PL_SSI_ZMODE_ENABLED_ON_ZERO
		ssi.enabled=YES
	else
		tbt29_init=TBT29_NO_SSI_AVAILABLE
	end if

	'check the TBT29_MFG_ID
	if tbt29_read_data(TBT29_REG_MFGID)<>TBT29_MFG_ID then
		tbt29_init=TBT29_WRONG_MFGID
		goto leave
	end if

	'check the TBT29_DEVID+MCP9808_DEFAULT_REVISION
	if tbt29_read_data(TBT29_REG_IDREV)<>TBT29_DEVID then
		tbt29_init=TBT29_WRONG_DEVID_REVISION
		goto leave
	end if

	'resolution :+0.25 C (tCONV = 65 ms typical)
	tbt29_write_data(TBT29_REG_RESOL,TBT29_RESOLUSION_MODE_1)
leave:

end function

function tbt29_get_c() as real
'Returns the signed floating point value expressing the temperature in deg. C.
	tbt29_get_c=tbt29_get()
	tbt29_get_c=tbt29_get_c/4
end function

function tbt29_get() as integer
'Returns the signed integer value expressing the temperature in 0.25 deg. C steps.	

	const DELAY_IN_65_MS=7
	dim r as word
	dim t_integer as word
	dim t_fraction as byte
	
	if tbt29_init_flag<>TBT29_INIT_SIGNATURE then

		exit function
	end if

	si2c_get(i2c_num_tbt29) 

	delay_msecond(DELAY_IN_65_MS)

	r=tbt29_read_data(TBT29_REG_TA)

	if r and &h1000 then
		'temperature is negative
		t_integer=(r and &h0FFF)/16
		t_fraction=(r and &h000F)/4
		tbt29_get=1024-((t_integer*4)+t_fraction)
		tbt29_get=tbt29_get*-1
	else	
		'temperature is positive
		t_integer=(r and &h0FFF)/16
		t_fraction=(r and &h000F)/4
		tbt29_get=(t_integer*4)+t_fraction
	end if
end function

function tbt29_read_data(op as tbt29_regs) as word
	dim upper,lower as byte=0

	upper=0
	lower=0
	si2c_get(i2c_num_tbt29)
	si2c_start()

	'send address + op
	si2c_write(TBT29_WRITE_ADDR)
	si2c_write(op)
	
	si2c_start()

	si2c_write(TBT29_READ_ADDR)

	if op=TBT29_REG_RESOL then
		lower=si2c_read(true)
	else
		upper=si2c_read(true)
		lower=si2c_read(false)
	end if

	si2c_stop()
	tbt29_read_data=upper*256+lower
end function

function tbt29_check() as ok_ng
	tbt29_check=NG
	dim check as byte
	dim upper,lower as byte=0

	upper=0
	lower=0
	si2c_get(i2c_num_tbt29)
	si2c_start()

	'send address + op
	si2c_write(TBT29_WRITE_ADDR)
	si2c_write(TBT29_REG_MFGID)
	
	si2c_start()

	si2c_write(TBT29_READ_ADDR)

	if TBT29_REG_MFGID=TBT29_REG_RESOL then
		lower=si2c_read(true)
	else
		upper=si2c_read(true)
		lower=si2c_read(false)
	end if

	si2c_stop()
 	
	check=upper*256+lower
	
	if check=TBT29_MFG_ID then
		tbt29_check=OK
	end if
	current_probe_info.reading=ftostr(tbt29_get_c(),FTOSTR_MODE_AUTO,4)
	current_probe_info.reading=parse_two_decimal_places(current_probe_info.reading)+"°C"
end function

sub tbt29_write_data(op as tbt29_regs,data as word)
	dim value as byte
	si2c_get(i2c_num_tbt29) 
	si2c_start()

	si2c_write(TBT29_WRITE_ADDR)
	si2c_write(op)

	if op=TBT29_REG_RESOL then
		value=data and &h00FF
		si2c_write(value)
	else
		value=(data and &hFF00)/256
		si2c_write(value)
		value=data and &h00FF
		si2c_write(value)		
	end if	
	si2c_stop()
end sub


function tbt30_init(use_ssi as no_yes) as ok_ng
'Call this function to initialize the Tibbit.
'Use_ssi argument specifies whether you want to communicate with this Tibbit using the SSI channel running in the I2C mode (YES), or
'go for direct I/O manipulation a.k.a. bit-banging (NO).
'Note that there is a limited number of SSI channels so if you set use_ssi=YES this function may return NG, meaning that
'there are no free SSI channels left. This doesn't mean failure. Rather, it means that comms will proceed in bit banging mood.
	tbt30_init_flag=TBT30_INIT_SIGNATURE
	i2c_num_tbt30=si2c_register("TBT30", DATA_PIN, CLK_PIN,use_ssi)

	if i2c_num_tbt30<4 then
		ssi.channel=i2c_num_tbt30
		ssi.enabled=NO
		ssi.baudrate=PL_SSI_BAUDRATES_400kHz
		ssi.clkmap=CLK_PIN
		ssi.dimap=DATA_PIN
		ssi.domap=DATA_PIN
		ssi.direction=PL_SSI_DIRECTION_LEFT
		ssi.mode=PL_SSI_MODE_2
		ssi.zmode=PL_SSI_ZMODE_ENABLED_ON_ZERO
		ssi.enabled=YES
	end if
	

	tbt30_init=tbt30_check()
	
end function

function tbt30_check() as ok_ng

	tbt30_check=NG
	dim b as byte
	dim readingsrawdata as byte(4)
	
	if tbt30_init_flag<>TBT30_INIT_SIGNATURE then
		exit function
	end if

	si2c_get(i2c_num_tbt30)

	'send the measurement request
	si2c_start()
	si2c_write(TBT30_WRITE_ADDR)
	si2c_stop()
	delay_msecond(DELAY_50MS)

	'fetch humidity data
	si2c_start()
	si2c_write(TBT30_READ_ADDR)
	readingsrawdata(0)=si2c_read(true)
	readingsrawdata(1)=si2c_read(true)
	readingsrawdata(2)=si2c_read(true)
	readingsrawdata(3)=si2c_read(false)
	si2c_stop()
	
	for b=0 to 3
		if readingsrawdata(b)<>255 then
			tbt30_check=OK
			exit for
		end if
	next b
	
	dim t,h as float
	dim temp, hum as string
	tbt30_get_pc(h,t)
	hum=ftostr(h,FTOSTR_MODE_AUTO,5)
	hum=parse_two_decimal_places(hum)+"%"
	temp=ftostr(t,FTOSTR_MODE_AUTO,5)
	if t<5 then
		current_probe_info.temp_warning=0
	else if t>50 then
		current_probe_info.temp_warning=1
	else 
		current_probe_info.temp_warning=2
	end if
	temp=parse_two_decimal_places(temp)+"°C "
	current_probe_info.reading = temp+hum


end function

function tbt30_get_pc(byref humidity as float, byref temperature as float) as ok_ng
'Returns "real" humidity and temperature data expressed in %PH and degrees C.
'NOTE: this call may fail, so check the ON/NG status returned by the function.
	
	tbt30_get_pc=OK
	
	dim temp,hum as word
	dim id as byte=&h00
	
	tbt30_get_pc=tbt30_get(hum,temp)
	
	humidity=hum
	humidity=(humidity*100)/16383

	temperature=temp
	temperature=temperature/16383
	temperature=temperature*165
	temperature=temperature-40
	
end function

function tbt30_get(byref humidity as word, byref temperature as word)as ok_ng
'Returns humidity and temperature data expressed as 16-bit unsigned values.
'See how the conversion into "real" values is done in tbt30_get_pc().
'NOTE: this call may fail, so check the ON/NG status returned by the function.

	dim status as tbt30_status
	dim data1,data2,data3,data4,tmp1,hi,lo,temp2 as byte
	
	if tbt30_init_flag<>TBT30_INIT_SIGNATURE then
	
		exit function
	end if

	tbt30_get=OK

	si2c_get(i2c_num_tbt30)

	'send the measurement request
	si2c_start()
	si2c_write(TBT30_WRITE_ADDR)
	si2c_stop()
	delay_msecond(DELAY_50MS)

	'fetch humidity data
	si2c_start()
	si2c_write(TBT30_READ_ADDR)
	data1=si2c_read(true)
	data2=si2c_read(true)
	data3=si2c_read(true)
	data4=si2c_read(false)
	si2c_stop()

	status=data1 and &h80
	status=status/128
	status=(status and &h40)/64


	select case status
	case TBT30_STATUS_NORMAL_OPERATION:

	case TBT30_STAUTS_STALE_DATA:

	case TBT30_STAUTS_IN_COMMAND_MODE:

		tbt30_get=NG
		exit function
	case TBT30_STAUTS_NOT_USED:

		tbt30_get=NG
		exit function
	case else:
	end select

	'humidity:   Data1 [13:6] + Data2 [7:0]
	'temerature: Data3 [13:6] + Data4 [5:0]
	'tmp1=data1*4
	'humidity=tmp1*64+data2
	humidity = (data1 and &h3F) *256 + data2
	
	tmp1=data3*4
	tmp1=tmp1/16
	hi=data3/64*16
	hi=hi+tmp1

	tmp1=data3 and &h03
	tmp1=tmp1*64
	
	temp2=data4 and &hC0
	temp2=temp2/4
	lo=tmp1+temp2

	tmp1=data4 and &h3C
	tmp1=tmp1/4
	lo=lo+tmp1

	temperature=hi*256+lo
	
end function


function tbt35_init(use_ssi as no_yes) as ok_ng
'Call this function to initialize the Tibbit.
'Use_ssi argument specifies whether you want to communicate with this Tibbit using the SSI channel running in the I2C mode (YES), or
'go for direct I/O manipulation a.k.a. bit-banging (NO).
'Note that there is a limited number of SSI channels so if you set use_ssi=YES this function may return NG, meaning that
'there are no free SSI channels left. This doesn't mean failure. Rather, it means that comms will proceed in bit banging mood.
	dim tmp as byte
	dim a0_tmp,b1_tmp,b2_tmp,c12_tmp,tmp2 as word

	tbt35_init_flag=TBT35_INIT_SIGNATURE
	i2c_num_tbt35=si2c_register("TBT35", DATA_PIN, CLK_PIN,use_ssi)

	if i2c_num_tbt35<4 then
		ssi.channel=i2c_num_tbt35
		ssi.enabled=NO
		ssi.baudrate=PL_SSI_BAUDRATES_400kHz
		ssi.clkmap=CLK_PIN
		ssi.dimap=DATA_PIN
		ssi.domap=DATA_PIN
		ssi.direction=PL_SSI_DIRECTION_LEFT
		ssi.mode=PL_SSI_MODE_2
		ssi.zmode=PL_SSI_ZMODE_ENABLED_ON_ZERO
		ssi.enabled=YES
	else
		if use_ssi=YES then
			tbt35_init=NG
		end if
	end if

	si2c_get(i2c_num_tbt35)

	si2c_start()

	si2c_write(TBT35_WRITE_ADDR)
	si2c_write(TBT35_CMD_READ_COEFFICIENT)

	si2c_start()

	si2c_write(TBT35_READ_ADDR)

	'a0
	tmp=si2c_read(true)
	a0_tmp=tmp*256
	tmp=si2c_read(true)
	a0_tmp=a0_tmp+tmp

	'b1
	tmp=si2c_read(true)
	b1_tmp=tmp*256
	tmp=si2c_read(true)
	b1_tmp=b1_tmp+tmp

	'b2
	tmp=si2c_read(true)
	b2_tmp=tmp*256
	tmp=si2c_read(true)
	b2_tmp=b2_tmp+tmp

	'c12
	tmp=si2c_read(true)
	c12_tmp=tmp*256
	tmp=si2c_read(false)
	c12_tmp=c12_tmp+tmp
	si2c_stop()

	'a0 :signed, integer bits=12,fractional bits=3
	a0=a0_tmp and &h7FF8
	a0=a0/8

	tmp2=a0_tmp and &h0007
	'Negative coefficients are coded in 2's complement notation
	if a0_tmp and &h8000 then
		a0=4095-a0
		tmp2=&h7-a0_tmp
	end if
	'f2
	if tmp2 and &h04 then
		a0=a0+0.5
	end if
	'f1
	if tmp2 and &h02 then
		a0=a0+0.25
	end if
	'f0
	if tmp2 and &h01 then
		a0=a0+0.125
	end if
	
	if a0_tmp and &h8000 then
		a0=-a0
	end if

	'b1	:signed, integer bits=2,fractional bits=13
	b1=b1_tmp and &h6000
	b1=b1/8192
	tmp2=b1_tmp and &h1FFF

	'Negative coefficients are coded in 2's complement notation
	if b1_tmp and &h8000 then
		b1=3-b1
		tmp2= &h1FFF-b1_tmp
	end if
	'f12
	if tmp2 and &h1000 then
		b1=b1+0.5
	end if
	'f11
	if tmp2 and &h800 then
		b1=b1+0.25
	end if
	'f10
	if tmp2 and &h400 then
		b1=b1+0.125
	end if
	'f9
	if tmp2 and &h200 then
		b1=b1+0.0625
	end if
	'f8
	if tmp2 and &h100 then
		b1=b1+0.03125
	end if
	'f7
	if tmp2 and &h80 then
		b1=b1+0.015625
	end if
	'f6
	if tmp2 and &h40 then
		b1=b1+0.0078125
	end if
	'f5
	if tmp2 and &h20 then
		b1=b1+0.00390625
	end if
	'f4
	if tmp2 and &h10 then
		b1=b1+0.001953125
	end if
	'f3
	if tmp2 and &h08 then
		b1=b1+0.0009765625
	end if
	'f2
	if tmp2 and &h04 then
		b1=b1+0.00048828125
	end if
	'f1
	if tmp2 and &h02 then
		b1=b1+0.000244140625
	end if
	'f0
	if tmp2 and &h01 then
		b1=b1+0.0001220703125
	end if

	if b1_tmp and &h8000 then
		b1=-b1
	end if

	'b2	:signed, integer bits=1,fractional bits=14
	b2=b2_tmp and &h4000
	b2=b2/16384
	tmp2=b2_tmp and &h3FFF

	'Negative coefficients are coded in 2's complement notation
	if b2_tmp and &h8000 then
		b2=1-b2
		tmp2=&h3FFF-b2_tmp
	end if

	'f13
	if tmp2 and &h2000 then
		b2=b2+0.5
	end if
	'f12
	if tmp2 and &h1000 then
		b2=b2+0.25
	end if
	'f11
	if tmp2 and &h800 then
		b2=b2+0.125
	end if
	'f10
	if tmp2 and &h400 then
		b2=b2+0.0625
	end if
	'f9
	if tmp2 and &h200 then
		b2=b2+0.03125
	end if
	'f8
	if tmp2 and &h100 then
		b2=b2+0.015625
	end if
	'f7
	if tmp2 and &h80 then
		b2=b2+0.0078125
	end if
	'f6
	if tmp2 and &h40 then
		b2=b2+0.00390625
	end if
	'f5
	if tmp2 and &h20 then
		b2=b2+0.001953125
	end if
	'f4
	if tmp2 and &h10 then
		b2=b2+0.0009765625
	end if
	'f3
	if tmp2 and &h08 then
		b2=b2+0.00048828125
	end if
	'f2
	if tmp2 and &h04 then
		b2=b2+0.000244140625
	end if
	'f1
	if tmp2 and &h02 then
		b2=b2+0.0001220703125
	end if
	'f0
	if tmp2 and &h01 then
		b2=b2+0.00006103515625
	end if

	if b2_tmp and &h8000 then
		b2=-b2
	end if

	'c12;signed, integer bits=0,fractional bits=13,dec pt zero pad=9
	tmp2=c12_tmp /4
	tmp2=tmp2 and &h1FFF

	'Negative coefficients are coded in 2's complement notation
	if c12_tmp and &h8000 then
		c12=-c12
		tmp2=&h1FFF-b2_tmp
	end if

	c12=0.0
	'f12
	if tmp2 and &h1000 then
		c12=c12+0.0009765625
	end if
	'f11
	if tmp2 and &h800 then
		c12=c12+0.00048828125
	end if
	'f10
	if tmp2 and &h400 then
		c12=c12+0.000244140625
	end if
	'f9
	if tmp2 and &h200 then
		c12=c12+0.0001220703125
	end if
	'f8
	if tmp2 and &h100 then
		c12=c12+0.00006103515625
	end if
	'f7
	if tmp2 and &h80 then
		c12=c12+0.000030517578125
	end if
	'f6
	if tmp2 and &h40 then
		c12=c12+0.0000152587890625
	end if
	'f5
	if tmp2 and &h20 then
		c12=c12+0.00000762939453125
	end if
	'f4
	if tmp2 and &h10 then
		c12=c12+0.000003814697265625
	end if
	'f3
	if tmp2 and &h08 then
		c12=c12+0.0000019073486328125
	end if
	'f2
	if tmp2 and &h04 then
		c12=c12+0.00000095367431640625
	end if
	'f1
	if tmp2 and &h02 then
		c12=c12+0.000000476837158203125
	end if
	'f0
	if tmp2 and &h01 then
		c12=c12+0.0000002384185791015625
	end if

	if c12_tmp and &h8000 then
		c12=-c12
	end if
	
	tbt35_init=tbt35_check()	

end function


function tbt35_check() as ok_ng

	tbt35_check=NG
	dim b as byte
	dim readingsrawdata as byte(4)
	
	

	if tbt35_init_flag<>TBT35_INIT_SIGNATURE then
		exit function
	end if

	si2c_get(i2c_num_tbt35)

	si2c_start()
	si2c_write(TBT35_WRITE_ADDR)
	si2c_write(TBT35_CMD_START_CONVERSION)
	si2c_write(&h00)
	si2c_stop()

	'Time between start convert command and data available in the Pressure and Temperature registers
	#if PLATFORM_TYPE_32
		delay_msecond(1)
	#else
		delay_msecond(3)
	#endif

	si2c_start()

	si2c_write(TBT35_WRITE_ADDR)
	si2c_write(TBT35_CMD_READ_PRESSURE)

	si2c_start()
	si2c_write(TBT35_READ_ADDR)

	readingsrawdata(0)=si2c_read(true)
	readingsrawdata(1)=si2c_read(true)
	readingsrawdata(2)=si2c_read(true)
	readingsrawdata(3)=si2c_read(false)
	si2c_stop()
	
	for b=0 to 3
		if readingsrawdata(b)<>255 and readingsrawdata(b)<>127 then
			tbt35_check=OK
			exit for
		end if
	next b
	current_probe_info.reading=ftostr(tbt35_get_kpa(),FTOSTR_MODE_AUTO,5)
	current_probe_info.reading=parse_two_decimal_places(current_probe_info.reading)+" kPa"

end function


function tbt35_get_kpa()as float
'Returns the floating point value expressing the pressure in kPa.
	dim tmp as byte
	dim padc,tadc as word=0
	dim c12x2,a1,a1x1,y1,a2x2,pcomp as float

	if tbt35_init_flag<>TBT35_INIT_SIGNATURE then
		exit function
	end if

	si2c_get(i2c_num_tbt35)

	si2c_start()
	si2c_write(TBT35_WRITE_ADDR)
	si2c_write(TBT35_CMD_START_CONVERSION)
	si2c_write(&h00)
	si2c_stop()

	'Time between start convert command and data available in the Pressure and Temperature registers
	#if PLATFORM_TYPE_32
		delay_msecond(1)
	#else
		delay_msecond(3)
	#endif

	si2c_start()

	si2c_write(TBT35_WRITE_ADDR)
	si2c_write(TBT35_CMD_READ_PRESSURE)

	si2c_start()
	si2c_write(TBT35_READ_ADDR)

	'pressure
	tmp=si2c_read(true)
	padc=tmp*256
	tmp=si2c_read(true)
	padc=padc+tmp

	'temprature
	tmp=si2c_read(true)
	tadc=tmp*256
	tmp=si2c_read(false)
	tadc=tadc+tmp

	si2c_stop()

	padc=padc /64
	tadc=tadc /64

	'Pcomp = a0 + (b1 + c12 .Tadc) . Padc + b2 . Tadc
	c12x2=c12*tadc
	a1=b1+c12x2
	a1x1=a1*padc
	y1=a0+a1x1
	a2x2=b2*tadc
	pcomp=y1+a2x2
	
	'kpa=pcomp*(115-50)/1023 +50
	tbt35_get_kpa=pcomp*(115-50)/1023+50

end function

function tbt36_init(use_ssi as no_yes)as ok_ng
'Call this function to initialize the Tibbit.
'Use_ssi argument specifies whether you want to communicate with this Tibbit using the SSI channel running in the I2C mode (YES), or
'go for direct I/O manipulation a.k.a. bit-banging (NO).
'Note that there is a limited number of SSI channels so if you set use_ssi=YES this function may return NG, meaning that
'there are no free SSI channels left. This doesn't mean failure. Rather, it means that comms will proceed in bit banging mood.

	tbt36_init=OK
	tbt36_init_flag=TBT36_INIT_SIGNATURE

	i2c_num_tbt36=si2c_register("TBT36", DATA_PIN, CLK_PIN,use_ssi)

	if i2c_num_tbt36<4 then
		ssi.channel=i2c_num_tbt36
		ssi.enabled=NO
		ssi.baudrate=PL_SSI_BAUDRATES_400kHz
		ssi.clkmap=CLK_PIN
		ssi.dimap=DATA_PIN
		ssi.domap=DATA_PIN
		ssi.direction=PL_SSI_DIRECTION_LEFT
		ssi.mode=PL_SSI_MODE_2
		ssi.zmode=PL_SSI_ZMODE_ENABLED_ON_ZERO
		ssi.enabled=YES
	else
		if use_ssi=YES then
			tbt36_init=NG
		end if
	end if

	si2c_get(i2c_num_tbt36)
	
	dim id as byte=&h00
	
	id=tbt36_get_reg(TBT36_REG_DEVID)

	if id<>TBT36_DEVID then
		tbt36_init=NG
		goto leave
	end if

	format=&h00
	#if TBT_36_RESOLUTION=TBT_36_RESOLUTION_FULL
		format=format or &h08
	#endif

	#if TBT_36_RANGE=TBT_36_RANGE_1P5G
		format=format or &h00
	#elif TBT_36_RANGE=TBT_36_RANGE_3G
		format=format or &h01
	#elif TBT_36_RANGE=TBT_36_RANGE_6G
		format=format or &h02
	#else
		'when format= 12G mode ,it should be full resolution mode
		format=format or &h0B
	#endif
	'set resolution + range
	tbt36_write_reg(TBT36_REG_DATA_FORMAT,format)
	'set measure bit
	tbt36_write_reg(TBT36_REG_POWER_CTL,&h08)
	'set as bypass mode
	tbt36_write_reg(TBT36_REG_FIFO_CTL,&h80)
leave:
	
end function

sub tbt36_get_mg(byref x_value as integer, byref y_value as integer, byref z_value as integer)
'Returns three signed values expressing acceleration in mG.	
	dim lx,ly,lz as long
	
	tbt36_get(x_value,y_value,z_value)
	lx=x_value
	ly=y_value
	lz=z_value
	x_value=(lx*2930)/100
	y_value=(ly*2930)/100
	z_value=(lz*2930)/100
end sub

function tbt36_check() as ok_ng
	
	dim id as byte=&h00
	
	id=tbt36_get_reg(TBT36_REG_DEVID)

	if id<>TBT36_DEVID then
		tbt36_check=NG
	else
		tbt36_check=OK
	end if
	
	dim x,y,z as integer
	tbt36_get_mg(x,y,z)
	current_probe_info.reading="x="+convert_accelerometer_values(x)+"g" + " y="+convert_accelerometer_values(y)+"g" + " z="+convert_accelerometer_values(z)+"g"
	
	
end function

function convert_accelerometer_values(value as string) as string
	
	dim val_i as integer
	dim i as byte
	dim len_val as byte
	dim negativ as boolean
	
	val_i = val(value)
	if val_i < 0 then
		val_i = val_i - 50	' For rounding
	else
		val_i = val_i + 50	' For rounding
	end if
	value = stri(val_i)
	len_val = len(value)
	
	if val_i < 0 then 
		negativ = true
		value = mid(value, 2, len_val - 1)
		len_val = len_val - 1
	else 
		negativ = false
	end if
	
	if len_val > 2 then
		convert_accelerometer_values = "." + mid(value, len_val - 2, 1)
	else 
		convert_accelerometer_values = ".0"
	end if
	
	if len_val > 3 then
		convert_accelerometer_values = mid(value, len_val - 3, 1) + convert_accelerometer_values
	else 
		convert_accelerometer_values = "0" + convert_accelerometer_values
	end if

	if len_val > 4 then
		convert_accelerometer_values = mid(value, len_val - 4, 1) + convert_accelerometer_values
	end if

	if negativ then
		if convert_accelerometer_values <> "0.0" then 
			convert_accelerometer_values = "-" + convert_accelerometer_values
		end if
	end if
	
end function

sub tbt36_get(byref x_value as integer, byref y_value as integer, byref z_value as integer)
'Returns three signed values expressing the acceleration.
'The scale is -4096 (-12G) to +4095 (+12G).
'Use tbt36_get_mg() to obtain acceleration in mG units.
	
	dim hi_byte,lo_byte as byte
	'dim x_hi,x_lo,y_hi,y_lo,z_hi,z_lo as byte
	dim x,y,z as word

	if tbt36_init_flag<>TBT36_INIT_SIGNATURE then 
		#if TBT36_DEBUG_PRINT=1
			adxl_debug_print("The lib is not initialized, call tbt36_init() first")
		#endif
		exit sub
	end if

	si2c_get(i2c_num_tbt36)
	tbt36_write_reg(TBT36_REG_POWER_CTL,&h08)

	'Read X axis value
	hi_byte=tbt36_get_reg(TBT36_REG_DATA_X1)
	lo_byte=tbt36_get_reg(TBT36_REG_DATA_X0)
	x=hi_byte*256+lo_byte

	'Read Y axis value
	hi_byte=tbt36_get_reg(TBT36_REG_DATA_Y1)
	lo_byte=tbt36_get_reg(TBT36_REG_DATA_Y0)
	y=hi_byte*256+lo_byte

	'Read Z axis value
	hi_byte=tbt36_get_reg(TBT36_REG_DATA_Z1)
	lo_byte=tbt36_get_reg(TBT36_REG_DATA_Z0)
	z=hi_byte*256+lo_byte

	dim tmp as byte
	'In bypass mode, the overrun bit is set when new data replaces unread data in the DATAX, DATAY, and DATAZ registers.
	tmp=tbt36_get_reg(TBT36_REG_INT_ENABLE)
	tmp=tmp and &hFE
	tbt36_write_reg(TBT36_REG_INT_ENABLE,tmp)

	tbt36_write_reg(TBT36_REG_POWER_CTL,&h00)
	#if TBT36_DEBUG_PRINT=1
		adxl_debug_print(hex(x)+"/"+hex(y)+"/"+hex(z))
	#endif

	#if TBT_36_RESOLUTION=TBT_36_RESOLUTION_NOMAL
		#if TBT_36_RANGE <>TBT_36_RANGE_12G
			const SIGN_MASK=&hF000
			const DATA_MASK=&h03FF
		#else
			const SIGN_MASK=&hF000
			const DATA_MASK=&h0FFF
		#endif
	#else
		'This is full resolution mode
		#if TBT_36_RANGE=TBT_36_RANGE_1P5G
			const SIGN_MASK=&hF000
			const DATA_MASK=&h03FF
		#endif

		#if TBT_36_RANGE=TBT_36_RANGE_3G
			const SIGN_MASK=&hF000
			const DATA_MASK=&h07FF
		#endif

		#if TBT_36_RANGE=TBT_36_RANGE_6G
			const SIGN_MASK=&hF000
			const DATA_MASK=&h0FFF
		#endif

		#if TBT_36_RANGE=TBT_36_RANGE_12G
			const SIGN_MASK=&hE000
			const DATA_MASK=&h1FFF
		#endif
	#endif

	if x and SIGN_MASK then
		x= x and DATA_MASK
		x_value=DATA_MASK-x
		x_value=(-x_value)
	else
		x_value=x and DATA_MASK
	end if

	if y and SIGN_MASK then
		y= y and DATA_MASK
		y_value=DATA_MASK-y
		y_value=(-y_value)
	else
		y_value= y and DATA_MASK
	end if

	if z and SIGN_MASK then
		z= z and DATA_MASK
		z_value=DATA_MASK-z
		z_value=(-z_value)
	else
		z_value=z and DATA_MASK
	end if
end sub

sub tbt36_write_reg(cmd as byte, data as byte)
	si2c_start()

	si2c_write(TBT36_WRITE_ADDR)

	si2c_write(cmd)
	si2c_write(data)

	si2c_stop()	
end sub

function tbt36_get_reg(cmd as byte) as byte
	dim tmp as byte
	si2c_start()

	si2c_write(TBT36_WRITE_ADDR)
	si2c_write(cmd)

	si2c_start()

	si2c_write(TBT36_READ_CODE)
	tmp=si2c_read(false)

	si2c_stop()

	tbt36_get_reg=tmp
end function

sub delay_msecond(value as word)
	sys.timercountms=0
	while sys.timercountms<value
	wend
end sub

function tbt30_get_reg(cmd as byte) as byte
	dim tmp as byte
	si2c_start()

	si2c_write(TBT30_WRITE_ADDR)
	si2c_write(cmd)

	si2c_start()

	si2c_write(TBT30_READ_ADDR)
	tmp=si2c_read(false)

	si2c_stop()

	tbt30_get_reg=tmp
end function